buildscript {
    ext {
        // Hilt causes a build error for an app with Kotlin 1.5.20.
        // https://github.com/google/dagger/issues/2684
        kotlinVersion = '1.5.21'
        navigationVersion = '2.3.5'
        hiltVersion = '2.38.1'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
        classpath 'com.google.gms:google-services:4.3.9'
    }
}

plugins {
    id 'org.jlleitschuh.gradle.ktlint' version '10.0.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    /*
     * In case "org.jetbrains.kotlin:kotlin-reflect" is a transitive dependency,
     * force it to be the same version of the Kotlin Gradle plugin..
     */
    configurations.all {
        resolutionStrategy.eachDependency {
            if (requested.group == 'org.jetbrains.kotlin') {
                useVersion kotlinVersion
            }
        }
    }
}

subprojects {
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    /*
     * Unless explicitly specified, the following default is used:
     * https://github.com/JLLeitschuh/ktlint-gradle/blob/master/plugin/src/main/kotlin/org/jlleitschuh/gradle/ktlint/KtlintExtension.kt
     */
    ktlint {
        android = true
        reporters {
            reporter(org.jlleitschuh.gradle.ktlint.reporter.ReporterType.CHECKSTYLE)
        }
        ignoreFailures = true // prevents the build from failing by warnings from ktlint.
    }
}

// Deletes the project-level "build" directory when clicking the menu bar > Build > Clean Project.
task clean(type: Delete) {
    delete rootProject.buildDir
}
