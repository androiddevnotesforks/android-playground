buildscript {
    ext {
        kotlinVersion = '1.4.31'
        navigationVersion = '2.3.4'
    }

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.2'
        classpath 'com.google.gms:google-services:4.3.5'
    }
}

plugins {
    id 'org.jlleitschuh.gradle.ktlint' version '10.0.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    ext.ReporterType = org.jlleitschuh.gradle.ktlint.reporter.ReporterType

    /*
     * Unless explicitly specified, the following default is used:
     * https://github.com/JLLeitschuh/ktlint-gradle/blob/master/plugin/src/main/kotlin/org/jlleitschuh/gradle/ktlint/KtlintExtension.kt
     */
    ktlint {
        android = true
        reporters {
            reporter(ReporterType.CHECKSTYLE)
        }
        ignoreFailures = true // prevents the build from failing by warnings from ktlint.
    }
}

// Deletes the project-level "build" directory when clicking the menu bar > Build > Clean Project.
task clean(type: Delete) {
    delete rootProject.buildDir
}
